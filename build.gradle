group 'com.lambda.gradle'
version System.getenv("BUILD_NUMBER")
def bucketName= "imc-0"

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 's3'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile (
            'com.amazonaws:aws-lambda-java-core:1.1.0',
            'com.amazonaws:aws-lambda-java-events:1.1.0'
    )
    testCompile 'junit:junit:4.12'
    compile group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1.1'
}

buildscript {
    dependencies {
        classpath 'com.github.skhatri:gradle-s3-plugin:1.0.4'
        classpath 'joda-time:joda-time:2.8.1'
    }
    repositories {
        mavenCentral()
    }
}

s3 {
    awsProfile = 'default'
    bucket = bucketName
    upload {
        file = "build/distributions/lambdaGradle-${version}.zip"
        key = "lambdaGradle-${version}.zip"
    }
}
task buildZip(type: Zip) {
    from compileJava
    from processResources
    into('lib') {
        from configurations.runtime
    }
}

test {
    jacoco {
        append = false
        destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
    }
}

build.dependsOn buildZip
